import groovy.xml.MarkupBuilder

repositories {
    jcenter()
}


configurations {
    compileOnly
}

sourceSets {
    main {
        compileClasspath += configurations.compileOnly
    }
    test {
        compileClasspath += configurations.compileOnly
    }
}





class GenerateMonitorConf extends DefaultTask {
    @Input
    String agentName = ""

    @Input
    String agentDescription = ""



    @Input
    String mainClass = ""

    @Input
    int sampleFreq  = 60

    @Input
    int timeout = 30

    @Input
    String classpath = ""

    @Input
    boolean continuous = false;
    @OutputFile
    File output  = "./build/conf/monitor.xml" as File


    @TaskAction
    public void generate() {
        output.withPrintWriter { writer ->
            def xml = new MarkupBuilder(writer)
            xml.monitor() {
                xml.name(agentName)
                xml.type('managed')
                xml.description(agentDescription)
                xml.'monitor-configuration'()
                xml.'monitor-run-task'() {
                    xml.'execution-style'((continuous)?'continuous':'periodic')
                    xml.'execution-frequency-in-seconds'(sampleFreq)
                    xml.name(agentName)
                    xml.'display-name'(agentName)
                    xml.description(agentDescription)
                    xml.type('java')
                    xml.'execution-timeout-in-secs'(timeout)
                    xml.'task-arguments'()
                    xml.'java-task'(){
                        xml.classpath("./lib/:"+classpath)
                        xml.'load-jars-in-classpath-dirs'("true")
                        xml.'impl-class'(mainClass)
                    }

                }
            }

        }
    }
}


task generateMonitorXML (type: GenerateMonitorConf) {

    agentName = project.name
    agentDescription = "Simple Agent Test"

    output = "$buildDir/conf/monitor.xml" as File

    classpath = jar.archiveName

}